plugins {
    id 'fabric-loom' version '1.0-SNAPSHOT'
    id 'maven-publish'
}

group 'me.videogamesm12'
version '2.0-pre3'

repositories {
    mavenCentral()
    maven { url "https://maven.terraformersmc.com/releases/" }
}

dependencies {
    minecraft "com.mojang:minecraft:1.17.1"
    mappings "net.fabricmc:yarn:1.17.1+build.65:v2"

    modImplementation "net.fabricmc.fabric-api:fabric-api:0.46.0+1.17"

    // Text components
    implementation include("net.kyori:adventure-text-serializer-gson:4.9.3")
    include("net.kyori:adventure-api:4.9.3")
    include("net.kyori:adventure-key:4.9.3")
    include("net.kyori:examination-api:1.3.0")

    subprojects.each {
        include project("${it.name}:")
    }
}

test {
    useJUnitPlatform()
}

loom {
    accessWidenerPath = file("src/main/resources/hotbarsplus.accesswidener")
}

allprojects {
    apply plugin: 'fabric-loom'
    apply plugin: 'maven-publish'

    tasks.withType(JavaCompile).configureEach {
        it.options.release.set(8)
    }

    repositories {
        maven {
            url "https://maven.shedaniel.me/"
        }
        maven {
            url "https://maven.terraformersmc.com/releases/"
        }
    }

    dependencies {
        modImplementation "net.fabricmc:fabric-loader:0.12.12"

        compileOnly 'org.projectlombok:lombok:1.18.22'
        annotationProcessor 'org.projectlombok:lombok:1.18.22'
    }

    processResources {
        inputs.property "version", rootProject.version
        filteringCharset "UTF-8"

        filesMatching("fabric.mod.json") {
            expand "version": rootProject.version
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier.set("sources")
        from sourceSets.main.allSource
    }
}

subprojects {
    setArchivesBaseName(String.format("HotbarsPlus_%s", project.getName()))

    version rootProject.version

    dependencies {
        implementation rootProject
    }
}

subprojects.each {
    remapJar.dependsOn("${it.path}:remapJar")
}