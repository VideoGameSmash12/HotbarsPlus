plugins {
    id 'fabric-loom' version '1.2-SNAPSHOT'
    id 'maven-publish'
}

group 'me.videogamesm12'
version '2.0-pre12'

repositories {
    mavenCentral()
    maven { url "https://maven.terraformersmc.com/releases/" }
    maven { url "https://jitpack.io" }
}

dependencies {
    minecraft "com.mojang:minecraft:1.20.3"
    mappings "net.fabricmc:yarn:1.20.3+build.1:v2"

    modImplementation "net.fabricmc.fabric-api:fabric-api:0.91.1+1.20.3"

    // Text components
    implementation include("net.kyori:adventure-text-serializer-gson:4.9.3")
    include("net.kyori:adventure-api:4.9.3")
    include("net.kyori:adventure-key:4.9.3")
    include("net.kyori:examination-api:1.3.0")

    implementation include("com.github.VideoGameSmash12:HotbarFailDetector:893ee75648fb6bc34b88708bfc8d177a5b145b03")

    subprojects.each {
        include project("${it.name}:")
    }
}

test {
    useJUnitPlatform()
}

loom {
    accessWidenerPath = file("src/main/resources/hotbarsplus.accesswidener")
}

allprojects {
    apply plugin: 'fabric-loom'
    apply plugin: 'maven-publish'

    tasks.withType(JavaCompile).configureEach {
        it.options.release.set(8)
    }

    repositories {
        maven {
            url "https://maven.shedaniel.me/"
        }
        maven {
            url "https://maven.terraformersmc.com/releases/"
        }
        maven {
            url "https://jitpack.io"
        }
    }

    dependencies {
        modImplementation "net.fabricmc:fabric-loader:0.12.12"
        implementation "net.kyori:adventure-api:4.9.3"

        compileOnly 'org.projectlombok:lombok:1.18.22'
        annotationProcessor 'org.projectlombok:lombok:1.18.22'
    }

    processResources {
        inputs.property "version", rootProject.version
        filteringCharset "UTF-8"

        filesMatching("fabric.mod.json") {
            expand "version": rootProject.version
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier.set("sources")
        from sourceSets.main.allSource
    }
}

subprojects {
    setArchivesBaseName(String.format("HotbarsPlus_%s", project.getName()))

    version rootProject.version

    dependencies {
        implementation rootProject
    }
}

subprojects.each {
    remapJar.dependsOn("${it.path}:remapJar")
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId rootProject.group
            artifactId rootProject.name
            version rootProject.version
            from components.java
        }
    }
}